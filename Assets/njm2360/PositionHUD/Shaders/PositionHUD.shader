Shader "Custom/PositionHUD"
{
    Properties
    {
        _BlockPx ("Block Size (px)", Float) = 1
        _OffsetX ("Offset X from left (px)", Float) = 0
        _OffsetY ("Offset Y from top (px)", Float) = 0
        _TextPx ("Text Pixel Size (px)", Float) = 2
        _ShowPixel ("Show Pixel Grid", Float) = 0
        _ShowText ("Show Text HUD", Float) = 0
    }
    SubShader
    {
        Tags { "Queue"="Overlay+1000" "RenderType"="Overlay" "IgnoreProjector"="True" }
        ZTest Always
        ZWrite On
        Cull Off
        Blend Off

        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #pragma target 5.0
            #include "UnityCG.cginc"

            float _BlockPx;
            float _OffsetX;
            float _OffsetY;
            float _TextPx;
            float _ShowPixel;
            float _ShowText;

            float _VRChatCameraMode;
            float _VRChatMirrorMode;
            uint  _VRChatTimeNetworkMs;

            struct appdata { float4 vertex : POSITION; float2 uv : TEXCOORD0; };
            struct v2f     { float4 pos : SV_Position; };

            v2f vert (appdata v)
            {
                v2f o;
                // Hide in cameras/mirrors, or when both parts are toggled off
                if (_VRChatCameraMode != 0.0 || _VRChatMirrorMode != 0.0
                    || (_ShowPixel < 0.5 && _ShowText < 0.5))
                {
                    o.pos = float4(2e6, 2e6, 2e6, 1.0);
                    return o;
                }
                float2 ndc = v.uv * 2.0 - 1.0;
                o.pos = float4(ndc, UNITY_NEAR_CLIP_VALUE, 1.0);
                return o;
            }

            #define ROWS 12
            #define COLS 32
            static const uint MAGIC = 0x5AC3E7A1u;

            // 5x7 pixel font (HD44780 style): one uint per row (5 bits, left pixel = bit4),
            // 7 rows per glyph, top row first
            #define G_MINUS 10u
            #define G_DOT   11u
            #define G_SP    12u
            #define G_X     13u
            #define G_Y     14u
            #define G_Z     15u
            #define G_P     16u
            #define G_H     17u
            #define G_R     18u
            static const uint FONT[19 * 7] = {
                0x0E, 0x11, 0x13, 0x15, 0x19, 0x11, 0x0E, // 0
                0x04, 0x0C, 0x04, 0x04, 0x04, 0x04, 0x0E, // 1
                0x0E, 0x11, 0x01, 0x02, 0x04, 0x08, 0x1F, // 2
                0x1F, 0x02, 0x04, 0x02, 0x01, 0x11, 0x0E, // 3
                0x02, 0x06, 0x0A, 0x12, 0x1F, 0x02, 0x02, // 4
                0x1F, 0x10, 0x1E, 0x01, 0x01, 0x11, 0x0E, // 5
                0x06, 0x08, 0x10, 0x1E, 0x11, 0x11, 0x0E, // 6
                0x1F, 0x01, 0x02, 0x04, 0x08, 0x08, 0x08, // 7
                0x0E, 0x11, 0x11, 0x0E, 0x11, 0x11, 0x0E, // 8
                0x0E, 0x11, 0x11, 0x0F, 0x01, 0x02, 0x0C, // 9
                0x00, 0x00, 0x00, 0x1F, 0x00, 0x00, 0x00, // -
                0x00, 0x00, 0x00, 0x00, 0x00, 0x0C, 0x0C, // .
                0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, // space
                0x11, 0x11, 0x0A, 0x04, 0x0A, 0x11, 0x11, // X
                0x11, 0x11, 0x11, 0x0A, 0x04, 0x04, 0x04, // Y
                0x1F, 0x01, 0x02, 0x04, 0x08, 0x10, 0x1F, // Z
                0x1E, 0x11, 0x11, 0x1E, 0x10, 0x10, 0x10, // P
                0x11, 0x11, 0x11, 0x1F, 0x11, 0x11, 0x11, // H
                0x1E, 0x11, 0x11, 0x1E, 0x14, 0x12, 0x11  // R
            };

            #define TXT_CHARS 10
            #define TXT_LINES 6

            // Fixed-width field "L -12345.67" (right-aligned, floating sign):
            // returns glyph index for char ci
            uint glyphAt(float v, uint label, uint ci)
            {
                if (ci == 0u) return label;
                if (ci == 7u) return G_DOT;
                uint s = min((uint)(abs(v) * 100.0 + 0.5), 9999999u);
                if (ci == 8u) return (s / 10u) % 10u;
                if (ci == 9u) return s % 10u;
                // ci 1..6: integer part
                uint div = 1u;
                for (uint k = ci; k < 6u; k++) div *= 10u; // 10^(6-ci)
                uint ip = s / 100u;
                if (ci >= 2u && (ci == 6u || ip >= div))
                    return (ip / div) % 10u;
                // no digit here: minus sits just left of the first digit
                if (v < 0.0 && (ci == 5u || ip >= div / 10u))
                    return G_MINUS;
                return G_SP;
            }

            fixed4 frag (v2f i) : SV_Target
            {
                float2 p = i.pos.xy;
                if (_ProjectionParams.x < 0.0)
                    p.y = _ScreenParams.y - p.y;

                // --- Top-left: binary telemetry grid ---
                float2 g = p - float2(_OffsetX, _OffsetY);
                float2 gridPx = float2(COLS, ROWS) * _BlockPx;

                if (_ShowPixel > 0.5 &&
                    g.x >= 0.0 && g.y >= 0.0 && g.x < gridPx.x && g.y < gridPx.y)
                {
                    uint col = (uint)(g.x / _BlockPx);
                    uint row = (uint)(g.y / _BlockPx);

                    float3 camPos = _WorldSpaceCameraPos;
                    float3 fwd = -UNITY_MATRIX_V[2].xyz;
                    float3 up  =  UNITY_MATRIX_V[1].xyz;

                    uint w[ROWS];
                    w[0]  = MAGIC;
                    w[1]  = _VRChatTimeNetworkMs;
                    w[2]  = asuint(camPos.x);
                    w[3]  = asuint(camPos.y);
                    w[4]  = asuint(camPos.z);
                    w[5]  = asuint(fwd.x);
                    w[6]  = asuint(fwd.y);
                    w[7]  = asuint(fwd.z);
                    w[8]  = asuint(up.x);
                    w[9]  = asuint(up.y);
                    w[10] = asuint(up.z);
                    w[11] = w[0]^w[1]^w[2]^w[3]^w[4]^w[5]
                          ^ w[6]^w[7]^w[8]^w[9]^w[10];

                    uint bit = (w[row] >> (31u - col)) & 1u;
                    return bit ? fixed4(1,1,1,1) : fixed4(0,0,0,1);
                }

                // --- Top-right: human-readable position / attitude HUD ---
                if (_ShowText < 0.5)
                    clip(-1.0);

                float2 cellPx = float2(6.0, 8.0) * _TextPx; // 5x7 glyph + 1px spacing
                float2 hudPx = float2(TXT_CHARS, TXT_LINES) * cellPx;
                float2 t = p - float2(_ScreenParams.x - _OffsetX - hudPx.x, _OffsetY);

                if (t.x < 0.0 || t.y < 0.0 || t.x >= hudPx.x || t.y >= hudPx.y)
                    clip(-1.0);

                uint ci = (uint)(t.x / cellPx.x);
                uint li = (uint)(t.y / cellPx.y);
                uint rx = (uint)(t.x / _TextPx) % 6u;
                uint ry = (uint)(t.y / _TextPx) % 8u;

                float3 camPos = _WorldSpaceCameraPos;
                float3 right = UNITY_MATRIX_V[0].xyz;
                float3 up    = UNITY_MATRIX_V[1].xyz;
                float3 fwd   = -UNITY_MATRIX_V[2].xyz;

                // Matches app/core/pose.py: P = pitch (up positive), H = heading/yaw, R = roll
                float pitch = degrees(asin(clamp(fwd.y, -1.0, 1.0)));
                float yaw   = degrees(atan2(fwd.x, fwd.z));
                float roll  = degrees(atan2(right.y, up.y));

                float vals[TXT_LINES]   = { camPos.x, camPos.y, camPos.z, pitch, yaw, roll };
                uint  labels[TXT_LINES] = { G_X, G_Y, G_Z, G_P, G_H, G_R };

                uint gph = glyphAt(vals[li], labels[li], ci);
                uint on = 0u;
                if (rx < 5u && ry < 7u)
                    on = (FONT[gph * 7u + ry] >> (4u - rx)) & 1u;
                return on ? fixed4(1,1,1,1) : fixed4(0,0,0,1);
            }
            ENDCG
        }
    }
    Fallback Off
}
